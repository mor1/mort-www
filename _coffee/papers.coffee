# Copyright (C) 2011-2015 Richard Mortier <mort@cantab.net>.  All Rights
# Reserved.

# Permission to use, copy, modify, and distribute this software for any purpose
# with or without fee is hereby granted, provided that the above copyright
# notice and this permission notice appear in all copies.

# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES WITH
# REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF MERCHANTABILITY
# AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY SPECIAL, DIRECT,
# INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES WHATSOEVER RESULTING FROM
# LOSS OF USE, DATA OR PROFITS, WHETHER IN AN ACTION OF CONTRACT, NEGLIGENCE OR
# OTHER TORTIOUS ACTION, ARISING OUT OF OR IN CONNECTION WITH THE USE OR
# PERFORMANCE OF THIS SOFTWARE.

wrap = (tag, x, cl, id) ->
  ids=''
  cls=''
  if id? then ids = " id='#{id}'"
  if cl? then cls = " class='#{cl}'"
  "<#{tag} #{ids} #{cls}>#{x}</#{tag}>"

div = (cl, x, id) -> wrap "div", x, cl, id
p = (cl, x) -> wrap "p", x, cl
span = (cl, x) -> wrap "span", x, cl
link = (cl, x, u) -> wrap "span", "<a href='#{u}'>#{x}</a>", cl

code = (m) ->
  switch m
    when "January" then "01"
    when "February" then "02"
    when "March" then "03"
    when "April" then "04"
    when "May" then "05"
    when "June" then "06"
    when "July" then "07"
    when "August" then "08"
    when "September" then "09"
    when "October" then "10"
    when "November" then "11"
    when "December" then "12"
    else "00"

entry = (as, e) ->
  authors = $.map(e.authors, (a,_) ->
    name = a
      .replace(/\b(\w)\w+ /, "$1. ")
      .replace(/(\w\.) (\w\.)/, "$1$2")
      .replace(/(\w\.) (\w\.)/, "$1$2")

    name = (if a of as then link name, name, as[a] else name)
    span "author", name
    ).join(", ")

  venue =
    switch e._type
      when "inproceedings" then e.booktitle
      when "article" then "#{e.journal}, #{e.volume} (#{e.number}): #{e.pages}"
      when "inbook" then "#{e.title} #{e.chapter}, #{e.publisher}"
      when "techreport" then "#{e.number}, #{e.institution}"
      when "patent" then e.number
      else ""

  date = if e.month then "#{e.month} #{e.year}." else "#{e.year}."

  links = "links: "
  links += if e.url? then link "url", "url", e.url else ""
  links += if e.html? then link "url html", "html", e.html else ""
  links +=
    if e.pdf?
    then link "url pdf", "pdf", "http://mor1.github.io/publications/#{e.pdf}"
    else ""
  links +=
    if e.doi? then link "url doi", "doi", "http://dx.doi.org/#{e.doi}"
    else ""

  ttag =
    switch e._type
      when "inproceedings" then "conference"
      when "article" then "journal"
      when "inbook" then "chapter"
      when "techreport" then "tr"
      when "patent" then "patent"
      else ""
  tags = [span 'tag', ttag].concat(e.tags?.map((t) -> span 'tag', t)).join('; ')

  issn = if e.issn? then "issn: #{span 'issn', e.issn}" else ""
  publisher = if e.publisher then span "publisher", "#{e.publisher}." else ""
  address = if e.address then span "address", "#{e.address}." else ""
  note = if e.note then "#{e.note}. " else ""
  linkbar = div 'linkbar',
    "#{span 'links', links} #{issn} tags: #{span 'tags', tags}"

  div "paper #{e._venue}",
    "#{span 'title', e.title}<br>
      #{authors}<br>
      #{span "venue", venue}. #{note}<br>
      #{date} #{publisher} #{address}<br>
      #{linkbar}
      "

$(document).ready ->
  authors = $.getJSON "papers/authors.json"
  papers = $.getJSON "papers/papers.json"

  records = {}
  yms = []
  $.when(authors, papers).done ([as,...],[ps,...]) ->
    tool = link '', ps.tool.name, ps.tool.url

    tgt = $("#entries")
    tgt.html('').after(
      div 'tool', (p '', "Generated by #{tool} on #{ps.date}.")
      )

    $.each ps.records, (_, e) ->
      m = if e.month then code e.month.split(" ")[0] else "00"
      k = "#{e.year}-#{m}"
      records[k] = [] unless k of records
      records[k].push e

      yms.push k unless k in yms

    oy = ""
    yms.sort().reverse()
    $.each yms, (_, ym) ->
      console.log ym
      y = ym.split("-")[0]
      if oy == "" or y != oy then (
        tgt.append (div 'break', '') unless oy == ""
        tgt.append (div "year", y, "y-#{y}")
      )
      oy = y

      $.each records[ym], (i, e) ->
        tgt.append (entry as, records[ym][i])
